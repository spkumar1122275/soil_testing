plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

def propertyFile = file(System.properties['user.home'] + '/.gradle/gradle.properties')
def properties = new Properties()
if (propertyFile.exists()) {
    properties.load(new FileInputStream(propertyFile))
}

android {
    compileSdkVersion 32
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        applicationId "io.ffem"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 278
        versionName "Beta 2.74"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"

    productFlavors {
        soil {
            applicationIdSuffix ".soil"
        }
        water {
            applicationIdSuffix ".water"
        }
    }

    sourceSets {
        soil {
            java.srcDirs = ['src/mainExt/java', 'src/soil/java']
        }
        water {
            java.srcDirs = ['src/mainExt/java', 'src/water/java']
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        appRelease {
            if (properties.getProperty('FFEM_RELEASE_STORE_FILE')) {
                storeFile file(FFEM_RELEASE_STORE_FILE)
                storePassword FFEM_RELEASE_STORE_PASSWORD
                keyAlias FFEM_RELEASE_KEY_ALIAS
                keyPassword FFEM_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            multiDexEnabled true
        }
        release {
            if (properties.getProperty('FFEM_RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.appRelease
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" +
                    (System.currentTimeMillis()) + "L)"
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "ffem-${variant.flavorName}-${variant.versionCode}.apk"
                }
            }
        }
    }

    defaultConfig {
        resConfigs 'en', 'en_US', 'hi', 'kn', 'te'
// do not change these defaults
        buildConfigField "String", "SUPPORT_EMAIL", "\"info@ffem.io\""
        buildConfigField "java.util.concurrent.atomic.AtomicBoolean", "TEST_RUNNING",
                "new java.util.concurrent.atomic.AtomicBoolean(false)"
        buildConfigField "Boolean", "TAKE_SCREENSHOTS", "false"
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + 0L + ")"
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    kotlinOptions { jvmTarget = "1.8" }
    namespace 'org.akvo.caddisfly'
    lint {
        abortOnError false
        checkReleaseBuilds true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'

    implementation 'com.google.android.play:core:1.10.3'

    // Math
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Architecture Components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.4.2'
    kapt 'androidx.room:room-compiler:2.4.2'
    kapt 'android.arch.persistence.room:compiler:1.1.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'com.jjoe64:graphview:4.2.2'

    // Email
    implementation files('libs/activation.jar')
    implementation files('libs/additionnal.jar')
    implementation files('libs/mail.jar')

    // Dagger
    kapt 'com.google.dagger:dagger-compiler:2.41'
    kapt 'com.google.dagger:dagger-android-processor:2.41'
    implementation 'com.google.dagger:dagger-android:2.41'
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.41'

    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Testing
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.4.0') {
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.7.3'

    // UIAutomator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    debugImplementation 'androidx.multidex:multidex:2.0.1'

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
}
repositories {
    mavenCentral()
}